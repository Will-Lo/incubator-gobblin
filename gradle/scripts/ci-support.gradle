import groovy.json.JsonOutput

/** Take inspiration from https://rnorth.org/faster-parallel-github-builds/
 * to generate gradle tasks to be picked up in the matrix by github actions.
 */

// Emit a JSON-formatted list of check tasks to be run in CI

def corePaths = ["gobblin-binary-management", "gobblin-compaction", "gobblin-core", "gobblin-data-management",
                 "gobblin-hive-registration", "gobblin-runtime", "gobblin-yarn", "gobblin-metrics-libs", "gobblin-runtime-hadoop"]
def servicePaths = ["gobblin-api", "gobblin-rest-service", "gobblin-restli"]
def modulePaths = ["gobblin-modules"]

task getGroupedTests {
    doLast {
        def taskNames = subprojects.findAll {
            subproject -> subproject.tasks.hasProperty('test')
        }
        def includedGroups
        switch(groupName) {
            case "Core Tests":
                includedGroups = taskNames.findAll {task ->
                    corePaths.any {
                        task.path.contains(it)
                    }
                }
                break;
            case "Service Tests":
                includedGroups = taskNames.findAll {task ->
                    servicePaths.any {
                        task.path.contains(it)
                    }
                }
                break;
            case "Module Tests":
                includedGroups = taskNames.findAll {task ->
                    modulePaths.any {
                        task.path.contains(it)
                    }
                }
                break;
            case "Other Tests":
                corePaths.addAll(servicePaths)
                corePaths.addAll(modulePaths)
                includedGroups = taskNames.findAll { task ->
                    !corePaths.any {
                        task.path.contains(it)
                    }
                }
                break;
            default:
                includedGroups = taskNames
                break;
        }

        def groupedTaskNames = includedGroups.collect { task -> task.tasks.findByName('test').getPath() }
        println "CI Task: " + groupedTaskNames.join(" ")
    }
}
